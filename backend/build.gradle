buildscript {
    ext {
        springBootVersion = '2.2.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/gradle/docker.gradle"

sourceCompatibility = 1.11

dependencies {
    implementation('org.springframework.boot:spring-boot-starter')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation('org.springframework.hateoas:spring-hateoas:1.0.0.BUILD-SNAPSHOT')
    implementation('org.springframework.plugin:spring-plugin-core:2.0.0.BUILD-SNAPSHOT')
    
    compileOnly('org.projectlombok:lombok:1.18.6')
    annotationProcessor('org.projectlombok:lombok:1.18.6')

    runtime('org.springframework.boot:spring-boot-devtools')
    runtime('com.h2database:h2')
}

task copyWebApp(type: Copy) {
    from('../frontend/dist') {
        include '**/*'
    }
    into 'build/resources/main/static'

    includeEmptyDirs = false
}

task copyHalExplorer(type: Copy) {
    from "$projectDir/../hal-explorer/dist"
    into "$buildDir/resources/main/static/hal-explorer"
}

bootJar {
    archiveBaseName = 'com.toedter.movies-demo'
    dependsOn copyWebApp
    dependsOn copyHalExplorer
}

copyWebApp.dependsOn ':frontend:build'
build.dependsOn createDockerfile, copyWebApp

